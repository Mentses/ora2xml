DECLARE
  TYPE TYPECURSOR IS REF CURSOR;
  CURSRC    TYPECURSOR;
  CURID     NUMBER;
  DESCTAB   DBMS_SQL.DESC_TAB;
  COLCNT    NUMBER;
  VNAME     VARCHAR2(50);
  VNUM      NUMBER;
  VDATE     DATE;
  SQLSTMT VARCHAR2(2000);
  V_TABLE_ID  VARCHAR2(20);
  V_CITYCODE  VARCHAR2(20);
  V_DATATIME  VARCHAR2(20);
BEGIN
  V_TABLE_ID := '&1';
  V_CITYCODE := '&2';
  V_DATATIME := '&3';
  SQLSTMT := 'SELECT * FROM T'||V_TABLE_ID||'0000000 WHERE CITYCODE = '''||V_CITYCODE||''' AND F'||V_TABLE_ID||'0100000 = '''||V_DATATIME||'''';
  -- 打开光标
  --DBMS_OUTPUT.PUT_LINE(SQLSTMT);
  OPEN CURSRC FOR SQLSTMT;
    
  -- 从本地动态SQL转换为DBMS_SQL
  CURID := DBMS_SQL.TO_CURSOR_NUMBER(CURSRC);
  -- 获取游标里面的数据列项数和每个数据列的属性，比如列名，类型，长度等
  DBMS_SQL.DESCRIBE_COLUMNS(CURID, COLCNT, DESCTAB);
  -- 定义列
  FOR I IN 1 .. COLCNT LOOP
    -- 此处是定义游标中列的读取类型，可以定义为字符，数字和日期类型，
    IF DESCTAB(I).COL_TYPE = 2 THEN
      DBMS_SQL.DEFINE_COLUMN(CURID, I, VNUM);
    ELSIF DESCTAB(I).COL_TYPE = 12 THEN
      DBMS_SQL.DEFINE_COLUMN(CURID, I, VDATE);
    ELSE
      DBMS_SQL.DEFINE_COLUMN(CURID, I, VNAME, 50);
    END IF;
  END LOOP;
  -- DBMS_SQL包获取行
  -- 从游标中把数据检索到缓存区（BUFFER）中，缓冲区 的值只能被函数COULUMN_VALUE()所读取
  DBMS_OUTPUT.PUT_LINE('<?xml version="1.0" encoding="GB2312"?>');
  DBMS_OUTPUT.PUT_LINE('<!-- Edited with ora2xml by yuxiao '||TO_CHAR(SYSDATE,'yyyy-mm-dd hh24:mi:ss'||' -->'));
  DBMS_OUTPUT.PUT_LINE('<H1>');
  WHILE DBMS_SQL.FETCH_ROWS(CURID) > 0 LOOP
    -- 函数COLUMN_VALUE()把缓冲区的列的值读入相应变量中。
    DBMS_OUTPUT.PUT_LINE('  <F'||V_TABLE_ID||'0000000>');
    FOR I IN 1 .. COLCNT LOOP
      IF DESCTAB(I).COL_TYPE = 1 THEN
        DBMS_SQL.COLUMN_VALUE(CURID, I, VNAME);
        DBMS_OUTPUT.PUT_LINE('    <'||DESCTAB(I).COL_NAME || '>' || VNAME || '</'||DESCTAB(I).COL_NAME || '>');
      ELSIF (DESCTAB(I).COL_TYPE = 2) THEN
        DBMS_SQL.COLUMN_VALUE(CURID, I, VNUM);
        DBMS_OUTPUT.PUT_LINE('    <'||DESCTAB(I).COL_NAME || '>'  || CASE WHEN VNUM < 1 AND VNUM > 0 THEN '0'||TO_CHAR(VNUM) ELSE TO_CHAR(VNUM) END || '</'||DESCTAB(I).COL_NAME || '>' );
      ELSIF (DESCTAB(I).COL_TYPE = 12) THEN
        DBMS_SQL.COLUMN_VALUE(CURID, I, VDATE);
        DBMS_OUTPUT.PUT_LINE('    <'||DESCTAB(I).COL_NAME || '>' || ' ' || TO_CHAR(VDATE, 'YYYY-MM-DD HH24:MI:SS') || '</'||DESCTAB(I).COL_NAME || '>' );
      END IF;
    END LOOP;
    DBMS_OUTPUT.PUT_LINE('  </F'||V_TABLE_ID||'0000000>');
  END LOOP;
  DBMS_OUTPUT.PUT_LINE('</H1>');
  DBMS_SQL.CLOSE_CURSOR(CURID);
END;
/
